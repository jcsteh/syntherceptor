# Syntherceptor
# SConstruct
# Copyright 2025 James Teh
# License: GNU General Public License version 2.0

import os
import winreg

vars = Variables()
vars.Add("version", "The version of this build", "unknown")
env = DefaultEnvironment(tools=[],
	variables=vars)

for arch, shortArch in (("x86", "x86"), ("x86_64", "x64")):
	archEnv = Environment(tools=["msvc", "mslink"],
		TARGET_ARCH=arch, HOST_ARCH=arch, shortArch=shortArch)
	archEnv.SConscript("src/archBuild_sconscript",
		exports={"env": archEnv},
		variant_dir="build/%s" % arch, duplicate=False)

def getMakensis():
	"""Get the path to makensis."""
	try:
		with winreg.OpenKey(winreg.HKEY_LOCAL_MACHINE, r"SOFTWARE\NSIS\Unicode",
				0, winreg.KEY_READ | winreg.KEY_WOW64_32KEY) as nsisKey:
			return os.path.join(winreg.QueryValueEx(nsisKey, None)[0], "makensis.exe")
	except WindowsError:
		pass
	try:
		with winreg.OpenKey(winreg.HKEY_LOCAL_MACHINE, r"SOFTWARE\NSIS",
				0, winreg.KEY_READ | winreg.KEY_WOW64_32KEY) as nsisKey:
			return os.path.join(winreg.QueryValueEx(nsisKey, None)[0], "makensis.exe")
	except WindowsError:
		pass
	return "makensis.exe"

installer = env.Command("build/syntherceptor_${version}.exe",
	["syntherceptor.nsi", "build/x86", "build/x86_64"],
	[[getMakensis(), "/V2",
	"/DOUTFILE=${TARGET.abspath}",
	"$SOURCE"]])
env.Default(installer)
